CCC$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
CCC              HETEROGENEOUS REACTION  RATE CODE  (FROM KEN CARSLAW)
CCC MNN  MOD 02/99     ADAPTED FOR 3-D MODEL
CCC MNN  MOD 02/99     MODIFICATIONS TO INCLUDE CORRECTIONS FROM
CCC                    HANSON'S RECENT PAPER (J.PHYS.CHEM. A 1998, 102,
CCC                    4794-4807).
CCC                    AT PRESENT NO PARAMETERIZATION IS AVAILABLE FOR INCLUDING
CCC                    THE EFFECT OF HNO3 ON THE REACTION PROBABILITY OF
CCC                    REACTIONS INVOLVING LIQUID AEROSOL (TERNARY)
CCC
CCC$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C DEAR TOPOZ MODELLER!  

C HERE IS THE MPICH CODE FOR CALCULATING THE COMPOSITION OF
C STRATOSPHERIC LIQUID AEROSOLS AND HETEROGENEOUS REACTION RATES. THE
C COMPOSITION CALCULATION PROGRAM IS CALLED 'ANALYTIC' AND IS IDENTICAL
C TO THAT WHICH IS OFFERRED IN OUR GRL PAPER, EXCEPT THAT WE HAVE ADDED
C HBR AND HOBR SOLUBILITIES.

C IN THE HETEROGENEOUS ROUTINE (CALLED 'HETERO') WE GIVE RATES OF
C SELECTED REACTIONS ON LIQUID AEROSOLS FOR WHICH THERE IS INFORMATION
C IN THE LITERATURE.  OTHER GAMMA VALUES SHOULD BE TAKEN FROM THE
C LATEST JPL COMPILATION.  THE MOST IMPORTANT REACTIONS ON NAT
C (CLNO3+HCL AND + H2O) ARE HUMIDITY DEPENDENT.  THERE IS NO SUCH
C INFORMATION FOR OTHER (LESS IMPORTANT) REACTIONS, SO THESE SHOULD 
C ALSO BE TAKEN FROM JPL.

C IMPORTANT NOTE!!  THE METHOD OF CALCULATING LIQUID REACTIONS FOLLOWS
C THE METHODOLOGY OF HANSON AND RAVISHANKARA. ALTHOUGH WE CALCULATE
C UPTAKE COEFFICIENTS (GAMMA VALUES) THIS DOES NOT MEAN THAT WE TREAT
C THE REACTIONS AS 'SURFACE' REACTIONS.  THE TRANSITION FROM 'BULK' TO
C 'SURFACE' REACTION IS ACCOUNTED FOR IN THE METHODOLOGY. NOTE THAT 
C SOME OF THE BROMINE REACTIONS HAVE ALREADY BEEN CALCULATED BY LARY ET
C AL (JGR).  THE CALCULATIONS GIVEN IN OUR CODE WILL YIELD DIFFERENT
C RESULTS, PARTICULARLY AT LOW TEMPERATURES.  THIS IS BECAUSE LARY'S
C METHOD OF CALCULATING BULK REACTION RATES DOES NOT ACCOUNT FOR GAS
C TRANSPORT LIMITATION, AND CAN YIELD REACTION RATES THAT ARE SEVERAL
C ORDERS OF MAGNITUDE TOO HIGH.

C NOTE THAT LIQUID AEROSOL GROWTH LEADS TO REMOVAL OF SPECIES FROM
C THE GAS PHASE, NOTABLY HNO3, HCL AND HBR. THE FRACTION REMAINING
C IN THE GAS PHASE IS CALLED 'PART...'. THIS SHOULD BE TAKEN INTO
C ACCOUNT WHEN CALCULATING ****GAS REACTION RATES*****.
C
C *****************************************
C ************ SCENARIOS ******************
C *****************************************
C AS INITIAL SCENARIOS TO RUN IN YOUR MODELS WE AGREED UPON: 
C (1) LIQUID ONLY (NO FREEZING AT ALL)
C (2) A SOLID SCENARIO
C     T>TSAT - LIQUID ONLY EXISTS
C     TNAT<T<TSAT - SAT ONLY EXISTS
C     TICE<T<TNAT - NAT ONLY EXISTS
C     T<TICE - ICE ONLY EXISTS
C PRESUMABLY YOU ALREADY HAVE EXPRESSIONS FOR TNAT AND TICE. 
C TSAT CAN BE CALCULATED FROM:
C        TSAT=3236.D0/(11.502D0-LOG10(PW*760.D0))
C WHERE PW IS THE WATER PARTIAL PRESSURE (ATM)
C
C THIS IS VERY EASY TO IMPLEMENT. YOU ONLY NEED TO KNOW THE CURRENT
C TEMPERATURE, NOT ITS HISTORY. ALL INFORMATION NEEDED TO CALCULATE
C REACTION RATES ON THESE PARTICLES IS GIVEN IN THE CODE BELOW.
C *****************************************
C *****************************************
C *****************************************
C
C
C PLEASE READ THE OPENING COMMENTS, AND ESPECIALLY THE LIMITS OF THE
C CODE!! IN PARTICULAR, THE LOWER TEMPERATURE LIMIT IS 185 K.  FOR
C TEMPERATURES LESS THAN THIS WE SUGGEST ICE PARTICLES SHOULD FORM, AND
C NO FURTHER CALCULATIONS MADE AT THESE TEMPERATURES FOR LIQUID
C AEROSOLS.

C LET US KNOW OF ANY PROBLEMS IMPLEMENTING THE CODE.

C KEN CARSLAW, THOMAS PETER
C
C =====================================================================
C    FORTRAN77 CODE TO CALCULATE COMPOSITION OF AQUEOUS 
C    HNO3/H2SO4/HCL/HBR/HOCL/HOBR STRATOSPHERIC AEROSOLS 
C     (CARSLAW, LUO, PETER - GEOPHYS. RES. LETT., 1995)
C             CARSLAW@NIKE.MPCH-MAINZ.MPG.DE
C
C            DISTRIBUTION VERSION 2 (27 MAY 1996)
C      UPDATES FROM VERSION 1: HBR AND HOBR SOLUBILITIES         
C
C         KEN CARSLAW
C         MAX-PLANCK-INSTITUT FUER CHEMIE
C         POSTFACH 3060
C         MAINZ 55020
C         GERMANY
C         TEL: (49) (0)6131 305 333
C         FAX: (49) (0)6131 305 328
C
C =====================================================================
C HNO3/H2SO4 COMPOSITION BASED ON THERMODYNAMIC MODEL OF THE SYSTEM
C HCL/HBR/HNO3/H2SO4/H2O {CARSLAW ET AL, J. PHYS. CHEM., 1995 - 
C A THERMODYNAMIC MODEL OF THE SYSTEM HCL-HNO3-H2SO4-H2O, 
C INCLUDING SOLUBILITIES OF HBR, FROM <200K TO 328K. 
C (ALSO AVAILABLE IN FORTRAN77)}
C HCL SOLUBILITY PARAMETRISATION FROM LUO ET AL., GRL, 1995
C HOCL SOLUBILITY PARAMETRISATION FROM HUTHWELKER ET AL., 
C J. AT. CHEM., 1995
C
C *********************************************************************
C THE MODEL IS VALID FOR 2E-5 MB<PW<2E-3 MB 
C (PW IS THE WATER PARTIAL PRESSURE)
C THE UPPER TEMPERATURE LIMIT IS 240K (IT EXTRAPOLATES SMOOTHLY
C     TO HIGHER TEMPERATURES, BUT COMPOSITIONS ARE NOT RELIABLE)
C THE LOWER TEMPERATURE LIMIT IS 185K, OR TICE-3K, 
C     WHICH EVER IS THE HIGHER.
C HNO3 SOLUBILITIES ARE CALCULATED TO A MAXIMUM TEMPERATURE OF 215K
C SOLUBILITIES ARE CALCULATED ON A MOLALITY BASIS -
C (MOLES OF SOLUTE PER KG OF WATER)
C *********************************************************************
C
C THE SOLUBILITIES OF HCL,HOCL,HBR AND HOBR ARE ASSUMED NOT TO AFFECT 
C THE UPTAKE OF HNO3 OR H2O.
C THIS INTRODUCES ONLY SMALL ERRORS AT THE VERY LOWEST TEMPERATURES,
C BUT FOR A FULL CALCULATION WHERE INTERACTIONS BETWEEN ALL SPECIES 
C IN SOLUTION ARE CONSIDERED, USE THE MODEL OF CARSLAW ET AL., 
C GIVEN ABOVE.
C
C THE PROGRAM HAS BEEN WRITTEN SO THAT THE DIFFERENT STEPS ARE CLEAR,
C AND IS THEREFORE  NOT CODED FOR OPTIMUM CALCULATION SPEED.  
C THIS IS LEFT FOR THE USER TO DO IF DESIRED.
C =====================================================================
C VARIABLE AND CONSTANT DEFINITIONS
C
C HHOCL, HHCL ETC = EFFECTIVE HENRY'S LAW CONSTANTS IN HNO3-H2SO4-H2O 
C     SOLUTION (MOL/KG WATER/ATM)
C HNB, HSB = EFFECTIVE HENRY'S LAW CONSTANT OF HNO3 IN HNO3-H2O AND 
C     H2SO4-H2O SOLUTIONS (MOL/KG WATER/ATM)
C MS,MN,MCL,MHOCL = CONCENTRATIONS OF H2SO4, HNO3, HCL AND HOCL 
C     (MOLE/KG OF WATER)
C MSB,MNB = BINARY SOLUTION CONCENTRATIONS (MOLE/KG OF WATER)
C NS = TOTAL MOLES SULPHATE/M3 AIR (ASSUMED TO BE TOTALLY IN LIQUID
C     PHASE)
C PARTHNO3, PARTHCL ETC = FRACTION OF HNO3, HCL ETC REMAINING IN 
C     GAS PHASE AFTER PARTITIONING INTO LIQ (1=NO REMOVAL TO AEROSOLS)
C PTOT = TOTAL ATMOSPHERIC PRESSURE (MB)
C PN,PHCL ETC = HNO3,HCL ETC VAPOUR PRESSURE (CALCULATED AFTER
C     PARTITIONING OF HNO3 TO THE AEROSOL) (ATM)
C PN0,PHCL0 ETC = INITIAL HNO3, HCL ETC PARTIAL PRESSURE (ATM)
C QHNO3, QHCL ETC = TOTAL VOLUME MIXING RATIOS OF HNO3, HCL ETC 
C     (PPBV, PPMV ETC)
C PW = WATER PARTIAL PRESSURE (ATM)
C T = TEMPERATURE (K)
C TICE = ICE FROST POINT TEMPERATURE (K)
C VLIQ = SPECIFIC AEROSOL VOLUME (DIMENSIONLESS)
C WS, WN ETC = WEIGHT FRACTIONS OF H2SO4, HNO3 ETC IN THE AEROSOL
C =====================================================================
      SUBROUTINE ANALYTIC (T,PTOT,QH2O,QHNO3,QH2SO4,
     1 QHCL,QHBR,QHOCL,QHOBR,SAD,FPSC1,FPSC2,JPSC,CH2O,CHNO3,
     2 RN2O5H2O,RCLNO3H2O,RCLNO3HCL,RHOCLHCL,RHOBRHCL,
     3 RHBRHOBR,RHBRHOCL,RBRNO3H2O,RN2O5HCL)
      LOGICAL FLGSAT,FLGNAT,FLGICE
      REAL NS,MSB,MNB,MS,MN,MCL,MHOCL,MBR,MHOBR
      REAL QN(10),QS(10),KN(7),KS(7)
      PARAMETER (R=8.205E-5,CTOA=7.33771E21)
      PARAMETER (PWL=2.0E-8,PWU=2.0E-6)
      SAVE QN,QS,KN,KS
C =====================================================================
      DATA QN/14.5734,0.0615994,-1.14895,0.691693, -0.098863,
     >     0.0051579,0.123472,-0.115574,0.0110113,0.0097914/
      DATA QS/14.4700,0.0638795,-3.29597,1.778224,-0.223244,
     >     0.0086486,0.536695,-0.335164,0.0265153,0.0157550/
      DATA KN/-39.136,6358.4,83.29,-17650.0,198.53,-11948,-28.469/
      DATA KS/-21.661,2724.2,51.81,-15732.0,47.004,-6969.0,-4.6183/
C =====================================================================
CCC     *** SET FLAGS ***
      FLGSAT = .FALSE.
      FLGNAT = .TRUE.
      FLGICE  = .TRUE.
CCCCMNN0299  INITIALIZE RATE CONSTANTS
      RN2O5H2O=0.0
      RCLNO3H2O=0.0
      RCLNO3HCL=0.0
      RHOCLHCL=0.0
      RHOBRHCL=0.0
      RHBRHOBR=0.0
      RHBRHOCL=0.0
      RBRNO3H2O=0.0
      RN2O5HCL=0.0
      HNO3S = 0.0
      H2OS = 0.0
C      
      CONCM = (PTOT/1013.)*CTOA/T
      PW=QH2O*1.E-6*PTOT/1013.0
C
      PN0=PTOT/1013.0*QHNO3*1.E-9
C
CC
      QH2SO4 = 0.0
      CALL H2SO4_SAD (T,PTOT,CONCM,SAD,QH2O,QHNO3,QH2SO4,FLGSAT)
CC
      PH2SO4=PTOT/1013.*QH2SO4*1.0E-9
C ALL H2SO4 IS ASSUMED TO BE IN THE DROPLETS. YOU MIGHT
C WANT TO INPUT DIRECTLY THE MOLES H2SO4/M3, WHICH IS A MORE
C NORMAL UNIT FOR H2SO4 AMOUNTS, BUT REMEMBER THAT THIS WILL
C VARY WITH PRESSURE AND TEMPERATURE.  PINATUBO CAN BE SIMULATED
C BY MULTIPLYING QH2SO4 BY DIFFERENT FACTORS.
C
      PHCL0=PTOT/1013.0*QHCL*1.E-9
C
      PHBR0=PTOT/1013.0*QHBR*1.E-9
C     
      PHOCL0=PTOT/1013.0*QHOCL*1.E-9
C
      PHOBR0=PTOT/1013.0*QHOBR*1.E-9
C
C =====================================================================
C TOTAL MOLECULAR CONCENTRATIONS (MOLECULE CM-3)
            CHCL=PHCL0*CTOA/T
            CHBR=PHBR0*CTOA/T
            CHOCL=PHOCL0*CTOA/T
            CHOBR=PHOBR0*CTOA/T
CCCC ==================================================================
      NS=PTOT*100.0*QH2SO4*1.E-9/8.314/T
      TT=R*T*NS
      TR=1.0E4/T-43.4782608
      PR=LOG(PW)+18.4
      TICE=2668.70/(10.4310-(LOG(PW)+LOG(760.0))/LOG(10.0))
C     
CCCCMNN0299   LIMIT CHECK FOR TEMPERATURE AND PW
      IF((T.GE.TICE-3.0).AND.(T.GE.185.0) .AND. (PW.GE.PWL) .AND.
     1   (PW.LE.PWU))THEN
C THE H2SO4/H2O PURE SOLUTION CONCENTRATION
         XSB = 1.0/(2.0*(KS(3)+KS(4)/T))*( -KS(1)-KS(2)/T-
     >        ((KS(1)+KS(2)/T)**2-4.0*(KS(3)+KS(4)/T)*(KS(5)+KS(6)/T
     >        +KS(7)*LOG(T)-LOG(PW)))**0.5) 
         MSB=55.51*XSB/(1.0-XSB)
C ASSUME SOLUTION IS PURE H2SO4/H2O
         MS = MSB
         MN = 0.0
         WS = MSB*0.098076/(1.0+MSB*0.098076)
         WN = 0.0
         PN = PN0
         PARTHNO3=1.0
CCMNN0299
        WTS=AMAX1(41.0, WS*100.0)
        XMUH2SO4=4.184*(1.514E4-286.0*(WTS-40.0)+1.080*(WTS-40.0)**2
     1            -3941.0/(WTS-40.0)**0.1)
C       Equilibrium H2SO4 vapour pressure (atm)
        PH2SO4EQ=EXP(-10156.0/T + 16.2590 
     1                 - XMUH2SO4/(8.314*T))
C
CCCC  SWITCH OFF AEROSOL WHEN EQ. VAPOR PRESSURE > H2SO4 PARTIAL PRESSURE
        IF (PH2SO4 .LE. PH2SO4EQ) THEN
         MS = 0.0
         WS = 0.0
         VLIQ = 0.0
         ELSE
CCCCCCC
C THE HNO3/H2SO4/H2O SOLUTION COMPOSITION
CCC         FOR TEMPERATURE .LT. 215K AND HNO3 GT 1PPTV
         IF((T.LE.215.0).AND.(QHNO3.GT.1.0E-3))THEN
            HSB= QS(1)+QS(2)*TR**2+(QS(3)+QS(4)*TR+QS(5)*TR**2
     >           +QS(6)*TR**3)*PR + (QS(7)+QS(8)*TR+QS(9)*TR**2)
     >           *PR**2+QS(10)*TR*PR**3
            HSB=EXP(HSB)
CCCMNN0299  *** THE FOLLOWING EXPRESSION BLOWS UP AT T=211.91018
CCC         *** USE LINEAR EXPRESSION INSTEAD OF THE QUADRATIC
CCC         *** IF ABS(T-211.9102)<0.0002
           IF (ABS(T-211.9102) .LT.0.0002) THEN
            XNB = -(KN(5)+KN(6)/T+KN(7)*ALOG(T)-ALOG(PW))/
     1               (KN(1)+KN(2)/T)
            ELSE
            XNB = 1.0/(2.0*(KN(3)+KN(4)/T))*( -KN(1)-KN(2)/T-
     >           ((KN(1)+KN(2)/T)**2-4.0*(KN(3)+KN(4)/T)*(KN(5)+KN(6)/T
     >           +KN(7)*LOG(T)-LOG(PW)))**0.5) 
            ENDIF
            MNB=55.51*XNB/(1.0-XNB)
            HNB= QN(1)+QN(2)*TR**2+(QN(3)+QN(4)*TR+QN(5)*TR**2
     >           +QN(6)*TR**3)*PR + (QN(7)+QN(8)*TR+QN(9)*TR**2)
     >           *PR**2+QN(10)*TR*PR**3
            HNB=EXP(HNB)
            A=(TT*HNB*MNB**2 - TT*HSB*MNB*MSB - 2.0*MNB**2*MSB 
     >           + MNB*MSB**2 + HNB*MNB*MSB*PN0 - HSB*MSB**2*PN0)/
     >           (MNB**2 - MNB*MSB)
            B=MSB*(-2.0*TT*HNB*MNB+TT*HSB*MSB+MNB*MSB-HNB*MSB*PN0)/
     >           (MNB-MSB)
            C=(TT*HNB*MNB*MSB**2)/(MNB - MSB) 
CCMNN0699   MODIFICATION TO AVOID ZERO DIVISION OR NEGATIVE ARGUMENT FOR
CCC         SQUARE ROOT FUNCTION
            TRM1 = -2.0*A**3+9.0*A*B-27.0*C
            TRM2 = A**2-3.*B
            ARGMNT = 4.0*TRM2**3 - TRM1**2
            IF (TRM1 .NE. 0. .AND. ARGMNT .GE. 0.) THEN
             PHI= ATAN(SQRT(ARGMNT)/TRM1) 
             ELSE IF (TRM1 .NE. 0.) THEN
             PHI = 0.0
             ELSE
             PHI = ASIN(1.0)
             ENDIF
            PI = 2.0*ASIN(1.0)
            IF(PHI.LT.0.) PHI=PHI+PI
            MS=-1.0/3.0 *(A+2.0*SQRT(TRM2)*COS((PI+PHI)/3.0))
CCCMNN0299      MAKE SURE THAT MS .LE. MSB
            MS = AMAX1(AMIN1(MS,MSB),0.0)
            MN = AMAX1(MNB*(1.0-MS/MSB),0.)
            WS = MS*0.098076/(1.0+MS*0.098076+MN*0.063012)
            WN = MN*0.063012/(1.0+MS*0.098076+MN*0.063012)
            IF (MN .GT. 0.0) THEN
             PN = MN/(HNB*MN/(MN+MS)+HSB*MS/(MN+MS))
             ELSE
             PN=PN0
             ENDIF
            PARTHNO3 = AMIN1(PN/PN0,1.0)
          ENDIF
CCCMNN0299   SAVE CONDENSED HNO3 (LIQUID AEROSOL)
         HNO3S = CHNO3*(1.0-PARTHNO3)
         CHNO3 = CHNO3 - HNO3S
C TOTAL SPECIFIC LIQUID AEROSOL VOLUME (DIMENSIONLESS)
         IF (WS .GT. 0.0) THEN
          VLIQ=NS*98.076E-6/WS/DENSITY(WS,WN,T)
          ELSE
          VLIQ = 0.0
          ENDIF
C
C =====================================================================
C THE SOLUBILITY OF HCL AND HBR
C H* (MOL/KG/ATM) ADAPTED FROM LUO ET AL., GRL, 1995.
C CALCULATED CONCENTRATIONS ASSUME THAT HCL AND HBR ARE TRACE 
C COMPONENTS OF THE AEROSOL.
CCC
CCC         IF(PHCL0.GT.0.0)THEN
            HHCL = EXP(-(21.0+46.610*WN+4.0690*WS-4.8370*SQRT(WN)
     >           +2.1860*SQRT(WS)-63.00*WN**2-40.170*WN*WS-
     >           1.5710*WS**2)
     >           -1.0/T*(-7437.0-8327.80*WN+1300.90*WS+1087.20*SQRT(WN)
     >           -242.710*SQRT(WS)+18749.0*WN**2+18500.0*WN*WS
     >           +5632.0*WS**2)-LOG(WN+0.610*WS)
     >           -LOG(36.461/(1000.0+98.076*MS+63.012*MN)))*1.013E3
CC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CC
CCCCMNN0299 EXPRESSION FOR HHCL TAKEN FROM HANSON (1998) TO BE CONSISTENT
CCC         WITH THE GAMMA EVALUATION LATER.
CCC            HHCL = EXP(-9.021+(6922.-9800.*XSB)/T)*
CCC     1                 (0.363-XSB*(2.616-4.995*XSB))
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
            MCL=(1.0/R/T*PHCL0)/(NS/MS + 1.0/R/T/HHCL)
            WCL=MCL*0.036461/(1.0+0.098076*MS+0.063012*MN)
            PHCL=MCL/HHCL
            IF (PHCL0 .GT. 0.0) THEN
             PARTHCL=1.0-(PHCL0-PHCL)/PHCL0
             ENDIF
CCMNN0299     IF(PHBR0.GT.0.0)THEN
            HHBR = EXP(-(17.83+1.02*WN-1.08*WS+3.9*SQRT(WN)
     >           +4.38*SQRT(WS)-8.87*WN**2-17.0*WN*WS+3.73*WS**2)
     >           -1.0/T*(-8220.50-362.76*WN+658.93*WS-914.0*SQRT(WN)
     >           -955.3*SQRT(WS)+9976.6*WN**2+19778.5*WN*WS
     >           +7680.0*WS**2)-LOG(WN+0.410*WS)
     >           -LOG(80.918/(1000.0+98.076*MS+63.012*MN)))*1.013E3
            MBR=(1.0/R/T*PHBR0)/(NS/MS + 1.0/R/T/HHBR)
            WBR=MBR*0.080918/(1.0+0.098076*MS+0.063012*MN)
            PHBR=MBR/HHBR
            IF (PHBR0 .GT. 0.0) THEN
             PARTHBR=1.0-(PHBR0-PHBR)/PHBR0
             ENDIF
C =====================================================================
C THE SOLUBILITY OF HOCL AND HOBR
C H* HOCL (MOL/KG/ATM) FROM HUTHWELKER ET AL., J. AT. CHEM, 1995
C AS AN APPROXIMATION, HUTHWELKER ET AL. ASSUMED THAT H* DEPENDS 
C UPON TOTAL MOLALITY IN HNO3/H2SO4 SOLUTION
CCMNN0299         IF(PHOCL0.GT.0)THEN
            HHOCL=EXP(6.49460-(-0.041070+54.56/T)*(MS+MN)
     >           -5862.0*(1.0/298.150-1.0/T))
            MHOCL=(1.0/R/T*PHOCL0)/(NS/MS + 1.0/R/T/HHOCL)
            WHOCL=MHOCL*0.05246/(1.0+0.098076*MS+0.063012*MN)
            PHOCL=PHOCL0
C
C SOLUBILITY OF HOCL IS LOW ENOUGH TO IGNORE GAS PHASE REMOVAL
C 
C SOLUBILITY OF HOBR IS NOT KNOWN FOR ALL STRATOSPHERIC CONDITIONS.
C LIMITED DATA (HANSON AND RAVISHANKARA 1995 AT 210K, 60 WT% H2SO4
C INDICATE THAT HHOBR=APPROX 18*HHOCL.  FOR HOBR AN EFFECTIVE HENRY'S
C LAW CONSTANT =18*HHOCL IS USED.
            HHOBR=18.0*HHOCL
            MHOBR=(1.0/R/T*PHOBR0)/(NS/MS + 1.0/R/T/HHOBR)
            WHOBR=MHOBR*0.09691/(1.0+0.098076*MS+0.063012*MN)
            PHOBR=PHOBR0
           ENDIF
C =====================================================================
C =====================================================================
C ONLY THE PARAMETERS THAT ARE NEEDED IN 'HETERO' ARE INCLUDED IN THE
C VARIABLE LIST.  OUTPUT VARIABLES SHOULD BE INCLUDED IN THE LIST 
C AS NEEDED!
C
            CALL HETERO(PTOT,T,VLIQ,PW,CH2O,CHCL,CHBR,CHOCL,CHOBR,XSB,
     >           MS,MN,WS,WN,WCL,WBR,WHOCL,WHOBR,HHCL,HHBR,HHOCL,HHOBR,
     >           PARTHBR,QH2SO4,CONCM,CHNO3,HNO3S,PARTHNO3,FPSC1,JPSC,
     1           RN2O5H2O,RCLNO3H2O,RCLNO3HCL,RHOCLHCL,RHOBRHCL,
     2           RHBRHOBR,RHBRHOCL,RBRNO3H2O,TICE,FLGSAT,FLGNAT)
C
      ENDIF
CCCMNN0299   **  CALCULATE HETEROGENEOUS RATES ON ICE PARTICLES
          IF ((FLGICE) .AND. JPSC .GT. 0) 
     1     CALL HETICE(PTOT,T,CH2O,CHCL,CHBR,CHOCL,CHOBR,CONCM,
     1     CHNO3,HNO3S,H2OS,FPSC2,
     2     RN2O5H2O,RCLNO3H2O,RCLNO3HCL,RHOCLHCL,RHOBRHCL,
     3     RHBRHOBR,RHBRHOCL,RBRNO3H2O,RN2O5HCL)

       PARTHNO3 = CHNO3/(CHNO3+HNO3S)
C
      RETURN
      END
C ==================================================================
      SUBROUTINE HETERO(PTOT,T,VLIQ,PW,CH2O,CHCL,CHBR,CHOCL,CHOBR,
     > XSB, MS,MN,WS,WN,WCL,WBR,WHOCL,WHOBR,HHCL,HHBR,HHOCL,HHOBR,
     > PARTHBR,QH2SO4,CONCM,CHNO3,HNO3S,PARTHNO3,FPSC1,JPSC,
     1 RN2O5H2O,RCLNO3H2O,RCLNO3HCL,RHOCLHCL,RHOBRHCL,
     2 RHBRHOBR,RHBRHOCL,RBRNO3H2O,TICE,FLGSAT,FLGNAT)
C
C ROUTINE TO CALCULATE UPTAKE COEFFICIENTS (GAMMA VALUES).
C GAMMA VALUES ARE INDICATED BY VARIABLES WITH PREFIX 'G', FOR
C EXAMPLE GHOCLHCL IS THE GAMMA VALUE OF HOCL DUE TO REACTION WITH
C HCL IN THE DROPLETS.
C FROM THE GAMMA VALUES, SECOND ORDER RATE CONSTANTS ARE CALCULATED.
C THESE HAVE THE PREFIX 'R' AND HAVE UNITS CM3 MOLECULE-1 S-1. FOR
C EXAMPLE, THE LOSS OF CLNO3 AND HCL DUE TO THE HETEROGENEOUS REACTION
C CLNO3+HCL -> CL2+HNO3 IS D(CLNO3)/DT (UNITS MOLECULE CM-3 S-1) = 
C -RCLNO3HCL.[CLNO3].[HCL], WHERE [CLNO3] AND [HCL] ARE THE
C ****TOTAL**** AMOUNTS OF THESE SPECIES IN UNITS MOLECULE CM-3.  
C
C
      LOGICAL FLGSAT,FLGNAT
      REAL MS,MN,KSUR,NDROP,NNAT
      REAL K1HOCLHCL,K2HOCLHCL,K1HOBRHCL,K2HOBRHCL,K1HBRHOBR,K2HBRHOBR
      REAL K1HBRHOCL,K2HBRHOCL
      PARAMETER (K2HOCLHCL=1.E5,K2HOBRHCL=1.E5,K2HBRHOBR=1.E7,
     >     K2HBRHOCL=1.E6,KSUR=576.0,ALPHA=0.30,RHO=2.E3,PI=3.1415)
      PARAMETER (CTOA=7.33771E21,AMU=1.66E-27,RHO1=1.60)
C
C
C ******************************************************
C THE FOLLOWING PARAMETERS ARE NEEDED IN THIS ROUTINE!!
C ******************************************************
C
C RNAT=MEAN RADIUS OF NAT PARTICLES (CM)
C NNAT=NUMBER OF NAT PARTICLES PER CM3 AIR
C RMEAN=MEAN RADIUS OF LIQUID AEROSOL DISTRIBUTION (CM)
C ALIQ=TOTAL AREA OF LIQUID AEROSOLS (CM2 CM-3 AIR)
C ASAT=TOTAL AREA OF SAT PARTICLES (CM2 CM-3 AIR)
C
C THESE MAY ALREADY BE CALCULATED ELSEWHERE IN YOUR MODEL.
C
C **** LIQUID AND SAT AEROSOL PARAMETERS ***
C
C RMEAN=MEAN RADIUS OF LIQUID DROPLETS (UNITS = CM)
C     THIS VARIES WITH THE TOTAL LIQUID VOLUME PER UNIT VOLUME OF AIR 
C     (CALLED VLIQ IN MAIN PROGRAM).  RMEAN CAN BE CALCULATED FROM VLIQ
C     USING 
C     
C     RMEAN=RMODE*EXP(0.5*(LOG(SIGMA))**2), WHERE
C 
C     RMODE=(3.0*VLIQ/(4.D0*PI*NDROP)*
C     EXP(-9.0/2.0*(LOG(SIGMA)**2)))**(1.D0/3.D0)
C
C     NDROP=NUMBER OF DROPLETS PER CM3 AIR
C     SIGMA=WIDTH OF LOGNORMAL (USE SIGMA=1.8).
C
C SAT CAN BE CALCULATED SIMILARLY, SEE BELOW
C
C *** NAT PARTICLE PARAMETERS ***
C
C RNAT CAN BE CALCULATED FROM:
C	RNAT=(3.0*(VNAT/NNAT)/4.0/PI)**(1.0/3.0)
C WHERE NNAT=NUMBER OF NAT PARTICLES PER UNIT VOLUME OF AIR 
C (UNITS=CM-3), AND VNAT = TOTAL NAT VOLUME (DIMENSIONLESS, 
C E.G., CM3 CM-3 AIR)
C
C 
      PARAMETER (NDROP=10.0,SIGMA=1.8,NNAT=10.0)
CCCCMNN299  GET VNAT (NAT VOLUME)
      RNAT = 0.
      IF (JPSC .GT. 0) THEN
        ZH2OT = (CH2O/CONCM)*PTOT*0.75
        ZH2OT=AMAX1(ZH2OT, 1.0E-12)
        ZTPSC = T
        ZMT=-2.7836 - 0.00088*ZTPSC
        ZBT=38.9855 - 11397.0/ZTPSC + 0.009179*ZTPSC
        ZHNO3EQ=10.0**(ZMT*ALOG10(ZH2OT) + ZBT)
C
C       Convert to number density
        ZHNO3EQ=ZHNO3EQ*1.333*CONCM/PTOT
C       Test for NAT particles
        TOTHNO3 = CHNO3 + HNO3S
        IF (TOTHNO3 .GT. ZHNO3EQ) THEN
          HNO3S = TOTHNO3 - ZHNO3EQ
          PARTHNO3 = ZHNO3EQ/TOTHNO3
          CHNO3 = ZHNO3EQ
C
C       Total NAT volume (dimensionless e.g. cm3/cm3 air)
          VNAT=117.0*AMU*HNO3S*1000.0/(RHO1)
          RNAT=(3.0*(VNAT/NNAT)/4.0/PI)**(1.0/3.0)
          FPSC1 = 1.0
C       disallow aerosol reactions if NAT present: zero aerosol liquid volume
          VLIQ = 0.
        ENDIF
      ENDIF
C NOW THE LIQUID PARAMETERS
      RMODE=(3.0*VLIQ/(4.D0*PI*NDROP)*
     >     EXP(-9.0/2.0*(LOG(SIGMA)**2)))**(1.D0/3.D0)
      RMEAN=RMODE*EXP(0.5*(LOG(SIGMA))**2)
      ALIQ=4.0*PI*RMODE**2*NDROP*EXP(2.0*(LOG(SIGMA))**2)
C NOW THE SAT PARAMETERS
      TSAT=3236.D0/(11.502D0-LOG10(PW*760.D0))
      CH2SO4=QH2SO4*PTOT*7.216D9/T
      VSAT=CH2SO4*170.D0/(6.D23*1.6D0)
      RMODESAT=(3.0*VSAT/(4.D0*PI*NDROP)*
     >     EXP(-9.0/2.0*(LOG(SIGMA)**2)))**(1.D0/3.D0)
      ASAT=4.0*PI*RMODESAT**2*NDROP*EXP(2.0*(LOG(SIGMA))**2)
C
C ICE FROST POINT TEMPERATURE
      DELT=T-TICE
C ICE SATURATION
      SICE=PW/10**(10.431-2668.70/T)*760.0

C
CCCCMNN0299    *** BEGIN LIQUID PHASE LOOP
      IF (VLIQ .GT. 0.0) THEN
C
C THE LIQUID PHASE DIFFUSION CONSTANT (CM2/S)
C DL = DIFFUSION CONSTANT OF HOCL IN H2SO4/HNO3 SOLUTION.  THIS IS
C USED ALSO FOR HBR AND HOBR.
      DL=DIFF(WS,WN,MS,MN,T)
C THE SOLUTION DENSITY (G/CM3)
      DENS=DENSITY(WS,WN,T)
C LIQUID PHASE CONCENTRATIONS (MOL DM-3 ATM-1)
      CHCLLIQ=WCL*DENS/0.036461
      CHOCLLIQ=WHOCL*DENS/0.05246 
      CHBRLIQ=WBR*DENS/0.08091 
      CHOBRLIQ=WHOBR*DENS/0.09691 
C CONVERT EFFECTIVE HENRY'S LAW CONSTANTS TO MOL/DM3/ATM
      FACTOR=DENS/(1.0+MS*0.098076+MN*0.063012) 
      HHCL=HHCL*FACTOR
      HHOCL=HHOCL*FACTOR
      HHOBR=HHOBR*FACTOR
      HHBR=HHBR*FACTOR
C FIRST ORDER RATE CONSTANTS FOR REACTION IN LIQUID - DM3 MOL-1 S-1
      K1HOCLHCL=K2HOCLHCL*CHCLLIQ
      K1HBRHOCL=K2HBRHOCL*CHOCLLIQ
      K1HBRHOBR=K2HBRHOBR*CHOBRLIQ
      K1HOBRHCL=K2HOBRHCL*CHCLLIQ
CCCCMNN0299
C =====================================================================
C ====================== CLONO2+HCL -> CL2+HNO3 =======================
C ====================== CLONO2+H2O -> HOCL+HNO3 ======================
C ========================= ON LIQUID AEROSOL =========================
C =====================================================================
C 
C TAKEN DIRECTLY FROM HANSON AND RAVISHANKARA, J. PHYS. CHEM., 
C 98, 5728, 1994, EXCEPT FOR THE FUNCTION F - SEE FOLLOWING COMMENTS -
C AND THE HCL SOLUBILITY, WHICH IS CALCULATED ACCORDING TO LUO ET AL.
C
C THE FUNCTION F: THE FORM OF F USED BY HANSON AND RAVISHANKARA CAN
C 'EXPLODE' UNDER CERTAIN CONDITIONS. IT HAS BEEN REPLACED HERE BY
C A STABLE FUNCTION THAT IS ACCURATE WITHIN ABOUT 4%. THIS IS ALSO
C THE CASE FOR OTHER REACTIONS THAT FOLLOW.
C      AH2O=1013.25*PW/(10**(9.217-2190.0/(T-12.70)))
C      G0=1.18E-4+9.1E-3*AH2O+0.50*AH2O**2
C      GS=AH2O*KSUR*CHCLLIQ
C      P=RHO*CHCLLIQ/AH2O
C      GCALC=G0*SQRT(1.0+P)
C      ADIVL=RMEAN/(1.4E-6*SQRT(1.0/AH2O))
C      F=(ADIVL+0.312*ADIVL**2)/(3.0+ADIVL+0.312*ADIVL**2)
C      GE=1.0/(1.0/(GS+F*GCALC)+1.0/ALPHA)
C      GCLNO3HCL=GE*(GS+F*GCALC*P/(1.0+P))/(GS+F*GCALC)
C      GCLNO3H2O=GE-GCLNO3HCL
C      IF (CHCL .GT. 0.0) RCLNO3HCL = 
C     1              0.25*GCLNO3HCL*1474.0*SQRT(T)*ALIQ/CHCL  
C      RCLNO3H2O=0.25*GCLNO3H2O*1474.0*SQRT(T)*ALIQ/CH2O
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCMNN1298  RECENT DATA FROM HANSON (J.PHYS.CHEM, 1998) USED
CCC         TABLE 8 FROM HANSON (1998)
        IF(ALIQ.GT.0.0) THEN
          GMR2 = EXP(-0.393-XSB*(13.13+50.914*XSB))
CCC
CCC       AH2O (H2O ACTIVITY FROM HANSON(1994))=BREL (FROM HANSON 1998)
CCC       THESE ARE EVALUATED AS A FUNCTION OF XSB
          BREL = 10.0**(0.151-XSB*(3.338+11.896*XSB))
          AH2O = BREL
          B0 = 257.0
          GS = B0 * BREL* CHCLLIQ
CCC
CCC       DIFFUSION COEFFICIENT FOR CLONO2 FRROM HANSON (1998)
          D1 = 10.0**(-7.815+XSB*(5.679-29.252*XSB))
          DREL = 1.0E8*D1
          A0 = 1.69E7
          RR1 = A0*DREL*CHCLLIQ
          RR2 = 10.0**(5.243-XSB*(5.749+25.348*XSB))
          GMB = GMR2*SQRT(1.+ RR1/RR2)
CCC
          ADIVL = RMEAN*SQRT((RR1+RR2)/D1)
          F=(ADIVL+0.312*ADIVL**2)/(3.0 + ADIVL + 0.312*ADIVL**2)
          SA = 10.0
          GMBPRM = F*GMB*SA/(F*GMB+SA)
          GE = 1.0/(1.0 + 1.0/(GMBPRM + GS))
          GCLNO3HCL =  GE*(GS+GMBPRM*RR1/(RR1+RR2))/(GS+GMBPRM)
          GCLNO3H2O = GE - GCLNO3HCL
          RCLNO3HCL = 0.25*GCLNO3HCL*1474.0*SQRT(T)*ALIQ/CHCL
          RCLNO3H2O = 0.25*GCLNO3H2O*1474.0*SQRT(T)*ALIQ/CH2O
        ELSE
          RCLNO3HCL=0.0
          RCLNO3H2O=0.0
        ENDIF
C =====================================================================
C ========================== HOCL + HCL ===============================
C ======================= ON LIQUID AEROSOL ===========================
C =====================================================================
C 
      IF (CHCLLIQ .GT. 0.0) THEN
       Q=RMEAN*SQRT(K1HOCLHCL/DL)
       FQ=(Q+0.312*Q**2)/(3.0+Q+0.312*Q**2)
       CBAR=2008.0*SQRT(T)         ![2008=SQRT(8.R/PI.MHOCL)]
       GHOCLHCL=FQ/(FQ+CBAR/(4.0*HHOCL*0.082*T*SQRT(K1HOCLHCL*DL)))
       RHOCLHCL=0.25*GHOCLHCL*CBAR*ALIQ/CHCL  
C ====================================================================
C =========================== HOBR + HCL =============================
C ======================= ON LIQUID AEROSOL ==========================
C ====================================================================
C
       Q=RMEAN*SQRT(K1HOBRHCL/DL)
       FQ=(Q+0.312*Q**2)/(3.0+Q+0.312*Q**2)
       CBAR=1477.0*SQRT(T)         ![1477=SQRT(8.R/PI.MHOBR)]
       GHOBRHCL=FQ/(FQ+CBAR/(4.0*HHOBR*0.082*T*SQRT(K1HOBRHCL*DL)))
       RHOBRHCL=0.25*GHOBRHCL*CBAR*ALIQ/CHCL  
      ENDIF
C =====================================================================
C ========================= HBR + HOCL/HOBR ===========================
C ======================= ON LIQUID AEROSOL ===========================
C =====================================================================
C
      IF (CHOBRLIQ .GT. 0.) THEN
       Q=RMEAN*SQRT(K1HBRHOBR/DL)
       FQ=(Q+0.312*Q**2)/(3.0+Q+0.312*Q**2)
       CBAR=1616.0*SQRT(T)         ![1616=SQRT(8.R/PI.MHBR)]
       GHBRHOBR=FQ/(FQ+CBAR/(4.0*HHBR*0.082*T*SQRT(K1HBRHOBR*DL)))
C RHBRHOBR IS MULTIPLIED BY PARTHBR TO ACCOUNT FOR GAS PHASE REMOVAL OF
C HBR TO THE DROPLETS.  THIS ENABLES TOTAL HBR TO BE USED IN RATE
C CALCULATION
       RHBRHOBR=0.25*GHBRHOBR*CBAR*ALIQ*PARTHBR/CHOBR  
      ENDIF
C
      IF (CHOCLLIQ .GT. 0.0) THEN
       Q=RMEAN*SQRT(K1HBRHOCL/DL)
       FQ=(Q+0.312*Q**2)/(3.0+Q+0.312*Q**2)
       GHBRHOCL=FQ/(FQ+CBAR/(4.0*HHBR*0.082*T*SQRT(K1HBRHOCL*DL)))
       RHBRHOCL=0.25*GHBRHOCL*CBAR*ALIQ*PARTHBR/CHOCL  
      ENDIF
C =====================================================================
C ======================== N2O5 + H2O =================================
C ====================== ON LIQUID AEROSOL ============================
C =====================================================================
C
      GN2O5H2O=0.1
      CBAR=1400.1*SQRT(T)
      RN2O5H2O=0.25*GN2O5H2O*CBAR*ALIQ/CH2O  
C =====================================================================
C ======================== BrONO2 + H2O ===============================
C ====================== ON LIQUID AEROSOL ============================
C =====================================================================
C FROM HANSON ET AL., JGR, 101, 9063-9069, 1996.
C =====================================================================
      GRXN = 211.0*AH2O**1.37
      GBRNO3H2O = (0.84*GRXN)/(GRXN + 0.84)
      CBAR=1221.4*SQRT(T)
      RBRNO3H2O=0.25*GBRNO3H2O*CBAR*ALIQ/CH2O  
C
CCCCMNN0299    *** END LIQUID PHASE LOOP
      ENDIF

CCCCMNN0299    *** BEGIN SAT LOOP
C =====================================================================
C ====================== CLNO3 + HCL ==================================
C ====================== CLNO3 + H2O ==================================
C ======================= ON SAT ======================================
C =====================================================================
C FOR CLNO3+HCL HANSON AND RAVISHANKARA (1993) FIT IS USED.  
C FOR CLNO3+H2O, GAMMA HAS BEEN FITTED AS A FUNCTION OF THE SATURATION 
C RATIO WITH RESPECT TO ICE FROM DATA OF ZHANG ET AL. (1994)
C
CCCMNN0299  SAT DATA FROM HANSON AND RAVI (1993) IS USED WITH THE
CCC         ADDITIONAL REQUIREMENT THAT SICE .LE. 1.0.
      IF ((FLGSAT) .AND. T .LT. TSAT .AND. RNAT .EQ. 0. 
     1             .AND. SICE .LE. 1.0) THEN
       GCLNO3HCLSAT=EXP(-0.636-0.4802*(T-TICE))
       GCLNO3H2OSAT=EXP(-7.41+2.94*SICE)
       GN2O5H2OSAT=0.006
       RCLNO3HCL=0.25*GCLNO3HCLSAT*1526.0*SQRT(T)*ASAT/CHCL  
       RCLNO3H2O=0.25*GCLNO3H2OSAT*1526.0*SQRT(T)*ASAT/CH2O  
       RN2O5H2O=0.25*GN2O5H2OSAT*1668.0*SQRT(T)*ASAT/CH2O  
CCCCMNN0299    *** END SAT LOOP
      ENDIF
C
CCCCMNN0299    *** BEGIN NAT LOOP
C =====================================================================
C ====================== CLNO3 + HCL ==================================
C ====================== CLNO3 + H2O ==================================
C ======================= ON NAT ======================================
C =====================================================================
C ==== REACTIONS FROM HANSON AND RAVISHANKARA DATA ====================
C FOR CLNO3+HCL THEIR FIT IS USED.  FOR CLNO3+H2O, GAMMA HAS BEEN
C FITTED AS A FUNCTION OF THE SATURATION RATIO WITH RESPECT TO ICE.
C
      IF ((FLGNAT) .AND. FPSC1 .GT. 0.) THEN
C GAMMA VALUES
      GCLNO3HCLNAT=1.0/(1.0/0.23+1.0/(0.7022*EXP(-0.518*DELT)))
      GCLNO3H2ONAT=EXP(-9.03+2.81*SICE)
      GN2O5H2ONAT=3.0E-4
      GHOCLHCLNAT=0.1

C BECAUSE NAT PARTICLES CAN BE VERY LARGE, GAS DIFFUSION LIMITATION
C IS TAKEN INTO ACCOUNT. THE SECOND ORDER RATE CONSTANTS ARE
C CALCULATED FROM THE EQUATION R=G*PI*R**2*CBAR*N/(1+3G.R/(4.L)), WHERE
C G=GAMMA, R=PARTICLE RADIUS, CBAR=MEAN MOLECULAR SPEED, L=MEAN FREE
C PATH (TURCO ET AL, JGR, 94, 16493, 1989).
C
      RCLNO3HCL=GCLNO3HCLNAT*4.56E4*SQRT(T/97.45)*RNAT**2*NNAT/
     >     (1.0+3.3E4*GCLNO3HCLNAT*RNAT*PTOT/T)/CHCL
      RHOCLHCL=GHOCLHCLNAT*4.56E4*SQRT(T/52.5)*RNAT**2*NNAT/
     >     (1.0+3.3E4*GHOCLHCLNAT*RNAT*PTOT/T)/CHCL
      RCLNO3H2O=GCLNO3H2ONAT*4.56E4*SQRT(T/97.45)*RNAT**2*NNAT/
     >     (1.0+3.3E4*GCLNO3H2ONAT*RNAT*PTOT/T)/CH2O
      RN2O5H2O=GN2O5H2ONAT*4.56E4*SQRT(T/110.0)*RNAT**2*NNAT/
     >     (1.0+3.3E4*GN2O5H2ONAT*RNAT*PTOT/T)/CH2O
      ENDIF
CCCCMNN0299   ***  END NAT LOOP
C
C
      RETURN
      END

C =====================================================================
      FUNCTION DENSITY(WS,WN,T)
C     DENSITY OF TERNARY SOLUTION IN G/CM3
C     WS ,WN ARE WT FRACTION, 
C     FITTED TO 0.05<WS+WN<0.70 WT FRACTION, BUT EXTRAPOLATES WELL 
C     185 < T (K) 
      REAL X(22)
      DATA X/
     >     2.393284E-02,-4.359335E-05,7.961181E-08,0.0,-0.198716351,
     >     1.39564574E-03,-2.020633E-06,0.51684706,-3.0539E-03,
     >     4.505475E-06,-0.30119511,1.840408E-03,-2.7221253742E-06,
     >     -0.11331674116,8.47763E-04,-1.22336185E-06,0.3455282,
     >     -2.2111E-03,3.503768245E-06,-0.2315332,1.60074E-03,
     >     -2.5827835E-06/
      save x
      W=WS+WN
      WH=1.0-W
      V1=X(1)+X(2)*T+X(3)*T**2+X(4)*T**3
      VS=X(5)+X(6)*T+X(7)*T**2+(X(8)+X(9)*T+X(10)*T**2)*W
     *     +(X(11)+X(12)*T+X(13)*T**2)*W*W
      VN=X(14)+X(15)*T+X(16)*T**2+(X(17)+X(18)*T+X(19)*T**2)*W
     *     +(X(20)+X(21)*T+X(22)*T**2)*W*W
      VMCAL=WH/18.0160*V1 + VS*WS/98.080 + VN*WN/63.0160
      DENSITY=1.0/VMCAL*0.001
C
      RETURN
      END
C =====================================================================
      FUNCTION DIFF(WS,WN,MS,MN,T)
      REAL MS,MN
C 	C:   WT % OF H2SO4
C	VISC: VISCOSITY  IN SI UNITS
C       T:   TEMPERATURE IN K.
C       DIFF: DIFFUSIVITY OF HOCL IN CM2 S-1
C CALCULATED USING THE HOUGHTON CUBIC CELL MODEL (SEE LUO ET AL, GRL
C 1994.
      C=WS*100.0
      A=EXP(-7.7221330+C*3.773159E-2)
      XB=623.80820+5.2216060*C-8.085769E-2*C**2 +2.1769575E-4*C**3
      T0=154.34660-0.95216940*C-2.6749929E-3*C**2+1.984055E-4*C**3
      XN=0.51860400
      VISS=1.E-3*A*T**XN*EXP(XB/(T-T0))
      A=0.026560+0.0019710*MN+0.00023760*MN**2
      XB=735.70
      T0=92.890+0.68480*MN
      XN=-0.012750*MN
      VISN=1.E-3*A*T**XN*EXP(XB/(T-T0))
      VISC=MS/(MS+MN)*VISS+MN/(MS+MN)*VISN
C VISCOSITY GIVEN AS MOLE RATIO OF H2SO4 AND HNO3.
C DIFFUSION COEFF FOR HOCL
C BASED ON HOUGHTON CUBIC CELL MODEL WITH CELL DIMENSION 3.65 ANGSTROMS
C NOTE THIS IS IN GOOD AGREEMENT (+-10% WITH A COMPOSITION DEPENDENT
C CELL DIMENSION AS GIVEN IN HUTHWELKER ET AL (J. AT. CHEM., 1995)
C
      DIFF=3.37E-14*T*DENSITY(WS,WN,T)/VISC
C	
      END
      SUBROUTINE HETICE(PTOT,T,CH2O,CHCL,CHBR,CHOCL,CHOBR,CONCM,
     1    CHNO3,HNO3S,H2OS,FPSC2,
     2    RN2O5H2O,RCLNO3H2O,RCLNO3HCL,RHOCLHCL,RHOBRHCL,
     3    RHBRHOBR,RHBRHOCL,RBRNO3H2O,RN2O5HCL)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     Calculate heterogeneous rates on ICE particles
C
C      N2O5   + H2O ->           2HNO3   (150  LaRC hr(1)
C      ClONO2 + H2O -> HOCl     + HNO3   (151  LaRC hr(2)
C      ClONO2 + HCl -> 2Cl      + HNO3   (152  LaRC hr(3)
C      N2O5   + HCl -> Cl + NO2 + HNO3   (153  LaRC hr(4)
C      HOCl   + HCl -> Cl + Cl  + H2O    (154  LaRC hr(5)
C      HOBr   + HBr -> Br + Br  + H2O    (155  LaRC hr(6)
C      HOBr   + HCl -> BrCl     + H2O    (156  LaRC hr(7)
C      HOCl   + HBr -> BrCl     + H2O    (157  LaRC hr(8)
C      BrONO2 + HBr -> Br + Br  + HNO3   (158
C      BrONO2 + HCl -> BrCl     + HNO3   (159
C      ClONO2 + HBr -> BrCl     + HNO3   (160
C      BrONO2 + H2O -> HOBr     + HNO3   (161  LaRC hr(9)
C      N2O5   + HBr -> Br + NO2 + HNO3   (162
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(LNT=1)
c
C     Number of ice particles cm-3
      REAL NICE,PI,SIGMA
      PARAMETER (NICE=10.0,SIGMA=1.8)
      PARAMETER (PI=3.14159)
C
      PARAMETER (ZDT2=0.0,AMU=1.66E-27,BOLTZ=1.381E-23)
C     Density of ice in g/cm3
      PARAMETER (RHO2=0.928)
      PARAMETER (AVOGAD=6.023E23)
C
      PARAMETER(GN2O5H2OICE=0.01,GCLNO3H2OICE=0.1,GCLNO3HCLICE=0.2,
     1  GN2O5HCLICE=0.03,GHOCLHCLICE=0.3,GHBRHOBRICE=0.1,
     2  GHOBRHCLICE=0.3,GBRNO3H2OICE=0.3)
C
C
        PPA = PTOT * 1.0E2
        ZTPSC=T + ZDT2
        ZTPSC=AMIN1(ZTPSC, 273.0)
        ZH2OEQ=610.78*EXP(21.875*(ZTPSC-273.16)/(ZTPSC-7.66))
C
C       Convert to number density
        ZH2OEQ=ZH2OEQ*CONCM/PPA
C
C       Test for ICE particles
        IF(CH2O.GT.ZH2OEQ) THEN
          FPSC2 = 1.0
C
C
          H2OS = CH2O - ZH2OEQ
          H2OS = AMAX1(H2OS, 0.0)
          CH2O = CH2O - H2OS
C
C         Reduce gas phase HNO3 by assuming NAT coating (as
C         opposed to liquid coating)
C
C         Calculate H2O in torr from remaining gas phase.
          ZH2OT=0.0075*PPA*CH2O/CONCM
          ZH2OT=AMAX1(ZH2OT, 1.0E-12)
          ZMT=-2.7836 - 0.00088*ZTPSC
          ZBT=38.9855 - 11397.0/ZTPSC + 0.009179*ZTPSC
          ZHNO3EQ=10.0**(ZMT*ALOG10(ZH2OT) + ZBT)
C
C         Convert to number density
          ZHNO3EQ=ZHNO3EQ*133.3*CONCM/PPA
          TOTHNO3 = CHNO3 + HNO3S
C
C         Condense HNO3
          IF (TOTHNO3 .GT. ZHNO3EQ) THEN
           HNO3S = TOTHNO3 - ZHNO3EQ
           HNO3S = AMAX1(HNO3S, 0.0)
           CHNO3 = TOTHNO3 - HNO3S
          ENDIF
C
C         Total ice volume (dimensionless e.g. cm3/cm3 air)
          VICE=18.0*AMU*H2OS*1000.0/(RHO2)
C
C         Mean radius of ice paticles (cm)
          RICE=(3.0*VICE/(NICE*4.0*PI))**(1.0/3.0)
C
C Because ice particles can be very large, gas diffusion limitation
C is taken into account. The first order rate constants are
C calculated from the equation K=G*PI*R**2*CBAR*N/(1+3G.R/(4.L)), where
C G=gamma, R=particle radius, CBAR=mean molecular speed, L=mean free
C path (Turco et al, JGR, 94, 16493, 1989).
C
          RICE2=RICE**2*NICE
          FREQC=4.56E4*SQRT(T/97.45)*RICE2
          FREQN=4.56E4*SQRT(T/110.0)*RICE2
          FREQH=4.56E4*SQRT(T/52.5)*RICE2
          FREQB=4.56E4*SQRT(T/142.0)*RICE2
          FREQO=4.56E4*SQRT(T/97.0)*RICE2
          RS4L =3.3E4*RICE*PTOT/T
C
          RN2O5H2O=GN2O5H2OICE*FREQN/(1.0+GN2O5H2OICE*RS4L)/CH2O
          RCLNO3H2O=GCLNO3H2OICE*FREQC/(1.0+GCLNO3H2OICE*RS4L)/CH2O
          RCLNO3HCL=GCLNO3HCLICE*FREQC/(1.0+GCLNO3HCLICE*RS4L)/CHCL
          RN2O5HCL=GN2O5HCLICE*FREQN/(1.0+GN2O5HCLICE*RS4L)/CHCL
          RHOCLHCL=GHOCLHCLICE*FREQH/(1.0+GHOCLHCLICE*RS4L)/CHCL
          RHBRHOBR=GHBRHOBRICE*FREQO/(1.0+GHBRHOBRICE*RS4L)/CHBR
          RHOBRHCL=GHOBRHCLICE*FREQO/(1.0+GHOBRHCLICE*RS4L)/CHCL
          RBRNO3H2O=GBRNO3H2OICE*FREQB/(1.0+GBRNO3H2OICE*RS4L)/CH2O
C
        ENDIF
C
      RETURN
      END
       subroutine h2so4_sad (t_in,p_in,concm,aero,qh2o_in,
     1  qhno3,qh2so4,flgsat)
c
CCCCMNN0299    ORIGINAL CODE FROM MARK HERVIG (RICH ECKMAN 11/98)
CCC            MODIFIED TO SUIT THE 3-D MODEL REQUIREMENT
CCC            THIS ROUTINE IS CALLED TO EVALUATE H2SO4 (PPBV)
CCC            FROM THE AEROSOL SURFACE AREA DENSITY.
CCC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
c 
        LOGICAL FLGSAT
        parameter (pi=3.14159)
        tsat=3236.0/(11.502-alog10(qh2o_in*1.0e-6*p_in/1013.0*760.0))
c
        ndrop=10
        sigma=1.8
        rmode=sqrt(aero*1.0e8/(4.0*pi*ndrop))*exp(-(alog(sigma)**2))
        vol_in=(ndrop*4.0*pi/3.)*rmode**3*exp(4.5*(alog(sigma))**2)
c
       	call h2so4_from_vol
     1  (p_in,t_in,vol_in,qh2o_in,qh2so4,qhno3)
c	
ccc     get qh2so4 assuming sat
        if ((flgsat) .and. t_in .lt. tsat) then
         ndrop=10
         sigma= 1.8
         rmodesat = sqrt(aero/(4.0*pi*ndrop))*exp(-(alog(sigma))**2)
         vsat = (4.0*pi*ndrop/3.0)*rmodesat**3*exp(4.5*(alog(sigma)**2))
         ch2so4 = vsat*6.0e23*1.6/170.
         qh2so4=ch2so4*1.0e9/concm
         endif
c
        return
	end
      subroutine h2so4_from_vol
     1 (p_in,t_in,vol_in,qh2o_in,qh2so4,qhno3)

c  find the h2so4 ammount from aerosol volume by interpolating linearly in
c  t - v "lta_equilib" is the Carslaw model to describe liquid ternary
c  h2o-hno3-h2so4 aerosol (LTA) the total h2so4 mixing ratio (qh2so4, ppbv)
c  is linear in sulfate aerosol volume density (vol_in, um3/cm3),  and the
c  environmental value is found by bounding qh2so4 by large and small values,
c  calculating the corresponding particulate volumes,  and interpolating to
c  vol_in.
c
c  in:  
c   p_in,  pressure, mb
c   t_in,  temperature, K
c   vol_in,  sulfate aerosol volume density, um3/cm3
c   qh2o_in,  water vapor mixing ratio, ppmv
c
c  out:
c   qh2so4, the total h2so4 mixing ratio, ppbv
c
c  source:  Mark Hervig,  University of Wyoming

      qh2so4a = 0.01
      qh2so4b = 10.0

      call lta_equilib(qh2so4a,qh2o_in,qhno3,t_in,p_in,va)
      call lta_equilib(qh2so4b,qh2o_in,qhno3,t_in,p_in,vb)
!      write(6,*)'va',va
!      call flush(6)
!      write(6,*)'vb',vb
!      call flush(6)
      if (va .gt. 0. .and. vb .gt. 0.) then
       dsdv   = (qh2so4b-qh2so4a)/(vb-va)
       qh2so4 = qh2so4b - dsdv*(vb-vol_in)
       qh2so4 = amax1(qh2so4,0.0)
       else
       qh2so4 = 0.0
       endif
      return
      end

      subroutine lta_equilib 
     #           (qh2so4,qh2o,qhno3,temp,ptot,v_lta)

c-------------------------------------------------------------------------
c
c FORTRAN77 CODE TO CALCULATE COMPOSITION OF AQUEOUS HNO3/H2SO4/HCL/HOCL
c STRATOSPHERIC AEROSOLS (CARSLAW, LUO, PETER - GEOPHYS. RES. LETT., 1995)
c
c HNO3/H2SO4 COMPOSITION BASED ON A PARAMETERISATION OF
c THE THERMODYNAMIC MODEL OF THE SYSTEM HCL/HBR/HNO3/H2SO4/H2O 
c {CARSLAW ET AL, J. PHYS. CHEM., 1995 - ALSO AVAILABLE IN FORTRAN77}
c HCL SOLUBILITY PARAMETERISATION FROM LUO ET AL., GRL, 1995
c HOCL SOLUBILITY PARAMETERISATION FROM HUTHWELKER ET AL., J. AT. SCI., 1995
c
c THE MODEL IS VALID FOR 2E-5 MB<PW<2E-3 MB (PW IS THE WATER PARTIAL PRESSURE)
c THE UPPER TEMPERATURE LIMIT IS 240K
c THE LOWER TEMPERATURE LIMIT IS 185K, OR TICE-3K, WHICH EVER IS THE HIGHER
c HNO3 SOLUBILITIES ARE CALCULATED TO A MAXIMUM TEMPERATURE OF 215K
c
c input
c
c  qh2so4.....total h2so4 (ppbv), usually about 0.5
c  qh2o.......h2o vapor mixing ratio (ppmv)
c  qhno3......hno3 vapor mixing ratio (ppbv)
c  temp.......atmospheric temperature (K)
c  ptot.......atmospheric pressure (mb)
c
c output
c
c  v_lta......volume of liquid ternery aerosol (lta) (um3/cm3)
c  ws.........weight % h2so4 in lta
c  wn.........weight % hno3 in lta
c  gf_hno3....gas phase fraction of hno3 after lta growth
c
c-------------------------------------------------------------------------

	REAL NS,MSB,MNB,MS,MN
	REAL QN(10),QS(10),KN(7),KS(7)

	DATA QN/14.5734,0.0615994,-1.14895,0.691693, -0.098863,
     >  0.0051579,0.123472,-0.115574,0.0110113,0.0097914/
	DATA QS/14.4700,0.0638795,-3.29597,1.778224,-0.223244,
     >  0.0086486,0.536695,-0.335164,0.0265153,0.0157550/
	DATA KN/-39.136,6358.4,83.29,-17650.0,198.53,-11948,-28.469/
	DATA KS/-21.661,2724.2,51.81,-15732.0,47.004,-6969.0,-4.6183/

c-------------------------------------------------------------------------

        t      = temp
        xw     = qh2o
        xmn    = qhno3
        xms    = qh2so4
        go to 100
        write(6,*)'temp',temp
        call flush(6)
        write(6,*)'qh2o',qh2o
        call flush(6)
        write(6,*)'qhno3',qhno3
        call flush(6)
        write(6,*)'qh2so4',qh2so4
        call flush(6)
        write(6,*)'ptot',ptot
        call flush(6)
100     continue
        if(xw<=1.e-20.or.t<=10.)then
          write(6,*)'error xw','ptot',ptot,xw,'t',t
          call flush(6)
        endif

	PW     = XW *1.E-6 *PTOT/1013.
	PN0    = PTOT/1013.0 *XMN *1.E-9
	NS     = PTOT *100. *XMS *1.E-9/8.314/T
	R      = 8.205E-5
	TT     = R*T*NS
	TR     = 1.E4*(1/T-1/230.)
	PR     = ALOG(PW)+18.4
        TICE=2668.70/(10.4310-(ALOG(PW)+ALOG(760.0))/ALOG(10.0))
 
        IF((T.GE.TICE-3.0).AND.(T.GE.185))THEN

c----     NEXT, THE H2SO4/H2O PURE SOLUTION CONCENTRATION

	  XSB = 1.0/(2.0*(KS(3)+KS(4)/T))*( -KS(1)-KS(2)/T-
     >    ((KS(1)+KS(2)/T)**2-4.0*(KS(3)+KS(4)/T)*(KS(5)+KS(6)/T
     >    +KS(7)*ALOG(T)-ALOG(PW)))**(1.0/2.0)) 
	  MSB=55.51*XSB/(1-XSB)

          IF((T.LE.215.0).AND.(PN0.GT.0.0))THEN

c----       CALCULATE HNO3/H2SO4/H2O SOLUTION COMPOSITION

            HSB= QS(1)+QS(2)*TR**2+(QS(3)+QS(4)
     >           *TR+QS(5)*TR**2+QS(6)*TR**3)
     >           *PR + (QS(7)+QS(8)*TR+QS(9)*TR**2)
     >           *PR**2+QS(10)*TR*PR**3
            HSB=EXP(HSB)


CCCMNN0299  *** THE FOLLOWING EXPRESSION BLOWS UP AT T=211.91018
CCC         *** USE LINEAR EXPRESSION INSTEAD OF THE QUADRATIC
CCC         *** IF ABS(T-211.9102)<0.0002
           IF (ABS(T-211.9102) .LT.0.0002) THEN
            XNB = -(KN(5)+KN(6)/T+KN(7)*ALOG(T)-ALOG(PW))/
     1               (KN(1)+KN(2)/T)
            ELSE
            XNB = 1.0/(2.0*(KN(3)+KN(4)/T))*( -KN(1)-KN(2)/T-
     >            ((KN(1)+KN(2)/T)**2-4.0*(KN(3)+KN(4)/T)
     >            *(KN(5)+KN(6)/T
     >            +KN(7)*ALOG(T)-ALOG(PW)))**(1.0/2.0)) 
            ENDIF
            MNB= 55.51*XNB/(1-XNB)
            HNB= QN(1)+QN(2)*TR**2+(QN(3)+QN(4)
     >           *TR+QN(5)*TR**2+QN(6)*TR**3)
     >           *PR + (QN(7)+QN(8)*TR+QN(9)*TR**2)
     >           *PR**2+QN(10)*TR*PR**3
            HNB=EXP(HNB)

            A  = (TT*HNB*MNB**2 - TT*HSB*MNB*MSB 
     >           - 2*MNB**2*MSB + MNB*MSB**2 + 
     >           HNB*MNB*MSB*PN0 - HSB*MSB**2*PN0)
     >           /(MNB**2 - MNB*MSB)
            B  = MSB*(-2*TT*HNB*MNB+TT*HSB*MSB+MNB
     >           *MSB-HNB*MSB*PN0)/(MNB-MSB)
            C  = (TT*HNB*MNB*MSB**2)/(MNB - MSB) 
            TRM1 = -2*A**3+9*A*B-27*C
            TRM2 = A**2-3*B
            ARGMNT = 4.*TRM2**3-TRM1**2
            IF (TRM1 .NE. 0. .AND. ARGMNT .GE. 0.) THEN
             PHI= ATAN(SQRT(ARGMNT)/TRM1)
             ELSE IF (TRM1 .NE. 0.) THEN
             PHI = 0.0
             ELSE
             PHI = ASIN(1.0)
             ENDIF
C
            PI = 2*ASIN(1.0)
	    IF(PHI.LT.0.) PHI=PHI+PI
            MS = -1./3. *(A+2*SQRT(TRM2)*COS((PI+PHI)/3.))
            MS = AMAX1(MS,0.0)
            MN = MNB*(1-MS/MSB)
            MN = AMAX1(MN,0.0)
            PN = MN/(HNB*MN/(MN+MS)+HSB*MS/(MN+MS))
            WS = MS*0.09812/(1+MS*0.09812+MN*0.06303)
            WN = MN*0.06303/(1+MS*0.09812+MN*0.06303)
            PARTHNO3 = (1.0-(PN0-PN)/PN0)
            gf_hno3  = parthno3
          ELSE

c----       ASSUME SOLUTION IS PURE H2SO4/H2O

            MS = MSB
            MN = 0.0
            WS = MSB*0.09812/(1+MSB*0.09812)
            WN = 0.0
            PN = PN0
            PARTHNO3=1.0
            gf_hno3 = parthno3
          ENDIF

c----     compute volume IN UM3/CM3

          v_lta = 0.0
          if (WS .gt. 0.0) v_lta=NS*98.076/WS/DENSITY(WS,WN,T)*1.e6
        else ! new ajl 10/13/2016 so always defined
          v_lta=0.0

        ENDIF

        return
	END
